apply plugin: 'com.android.model.application'

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    proc.text.trim().take takeFromHash
}

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId    = "com.mobilecg.androidapp"
            minSdkVersion.apiLevel    = 8
            targetSdkVersion.apiLevel = 23
        }
    }

    compileOptions.with {
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }

    android.ndk {
        moduleName = "ecg"
        cppFlags.add("-Werror")
        cppFlags.add("-DGIT_HASH=\""+getCheckedOutGitCommitHash()+"\"")
        ldLibs.addAll(["log", "GLESv2", "android"])
        stl        = "c++_static"
    }

    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles.add(file('proguard-rules.txt'))
        }
    }

    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters.add("armeabi-v7a")
        }
        create ("arm8") {
            ndk.abiFilters.add("arm64-v8a")
        }
        create ("x86-32") {
            ndk.abiFilters.add("x86")
        }
        // for detailed abiFilter descriptions, refer to "Supported ABIs" @
        // https://developer.android.com/ndk/guides/abis.html#sa

        // build one including all cpu architectures
        create("all")
    }
}

